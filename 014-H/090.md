KingNFT

high

# Attack on ````ErrorUtils```` library

## Summary
The ````ErrorUtils```` library is designed to parse ABI encoded ````bytes```` to ````string````, the ````bytes```` parameters come from both trusted GMX contracts and untrusted third-party contracts. The ````getRevertMessage()```` of ````ErrorUtils```` library is not implemented properly. The untrusted third-party contracts can provide carefully crafted ````bytes```` to trigger unexpected revert on GMX contracts.

## Vulnerability Detail
The issue arises on L22, attackers can construct  abnormal ````bytes```` parameter to make ````abi.decode()```` reverting.
```solidity
File: contracts\utils\ErrorUtils.sol
07:     function getRevertMessage(bytes memory result) internal pure returns (string memory, bool) {
08:         // If the result length is less than 68, then the transaction either panicked or failed silently
09:         if (result.length < 68) {
10:             return ("", false);
11:         }
12: 
13:         bytes4 errorSelector = getErrorSelectorFromData(result);
14: 
15:         // 0x08c379a0 is the selector for Error(string)
16:         // referenced from https://blog.soliditylang.org/2021/04/21/custom-errors/
17:         if (errorSelector == bytes4(0x08c379a0)) {
18:             assembly {
19:                 result := add(result, 0x04)
20:             }
21: 
22:             return (abi.decode(result, (string)), true); // @audit trigger revert
23:         }
24: 
25:         // error may be a custom error, return an empty string for this case
26:         return ("", false);
27:     }
```
To verify the issue, here is a minor modification from ````library```` to ````contract````
```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.19;

contract ErrorUtils {
    function getRevertMessage(bytes memory result) public pure returns (string memory, bool) {
        // If the result length is less than 68, then the transaction either panicked or failed silently
        if (result.length < 68) {
            return ("", false);
        }

        bytes4 errorSelector = getErrorSelectorFromData(result);

        // 0x08c379a0 is the selector for Error(string)
        // referenced from https://blog.soliditylang.org/2021/04/21/custom-errors/
        if (errorSelector == bytes4(0x08c379a0)) {
            assembly {
                result := add(result, 0x04)
            }

            return (abi.decode(result, (string)), true);
        }

        // error may be a custom error, return an empty string for this case
        return ("", false);
    }

    function getErrorSelectorFromData(bytes memory data) internal pure returns (bytes4) {
        bytes4 errorSelector;

        assembly {
            errorSelector := mload(add(data, 0x20))
        }

        return errorSelector;
    }
}
```
Deploy it to https://remix.ethereum.org/
When we call ````getRevertMessage()```` with a normal ````bytes```` such as
```solidity
0x08c379a00000000000000000000000000000000000000000000000000000000000000020000000000000000000000000000000000000000000000000000000000000000b474d5820636f6e74657374000000000000000000000000000000000000000000
```
It will successfully return
```solidity
0:
string: GMX contest
1:
bool: true
```
If we change the fifth byte ````00```` to ````ff````
```solidity
0x08c379a0ff00000000000000000000000000000000000000000000000000000000000020000000000000000000000000000000000000000000000000000000000000000b474d5820636f6e74657374000000000000000000000000000000000000000000
```
now the call will revert.

## Impact
The ````getRevertMessage()```` function is widely called, such as by ````afterOrderExecution()````, ````afterOrderFrozen()````, and ````afterDepositExecution()````. Attackers can easily control a revert to favor them but be harmful to the system.

## Code Snippet
https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/utils/ErrorUtils.sol#L22

## Tool used

Manual Review

## Recommendation
Strictly check the ````bytes```` parameter such as
```diff
File: contracts\utils\ErrorUtils.sol
07:     function getRevertMessage(bytes memory result) internal pure returns (string memory, bool) {
08:         // If the result length is less than 68, then the transaction either panicked or failed silently
09:         if (result.length < 68) {
10:             return ("", false);
11:         }
12: 
13:         bytes4 errorSelector = getErrorSelectorFromData(result);
14: 
15:         // 0x08c379a0 is the selector for Error(string)
16:         // referenced from https://blog.soliditylang.org/2021/04/21/custom-errors/
17:         if (errorSelector == bytes4(0x08c379a0)) {
+               uint256 lengthWithoutSelector = result.length - 4;
+               uint256 strLength;
+               uint256 offset;
18:             assembly {
19:                 result := add(result, 0x04)
+                   offset := mload(add(result, 0x20))
+                   strLength := mload(add(result, 0x40))
20:             }
21: 
-22:            return (abi.decode(result, (string)), true); // @audit trigger revert
+               if (offset == 0x20 && lengthWithoutSelector - 0x40 >= strLength) {
+                   return (abi.decode(result, (string)), true);
+               }
23:         }
24: 
25:         // error may be a custom error, return an empty string for this case
26:         return ("", false);
27:     }
```