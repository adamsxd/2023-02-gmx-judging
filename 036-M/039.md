koxuan

high

# DOS to executeDeposit if diff is more than longTokenAmount

## Summary
When user deposit in a market with the same long and short tokens, the adjusted amount will be calculated to deposit in a way whereby the least price impact will be done. However, an edge case in `getAdjustedLongAndShortTokenAmounts` will cause `executeDeposit` to always revert when difference of pool long token amount and pool short token amount is more than user deposit amount. Note that this requires another bug to be fixed that will always cause getAdjustedLongAndShortTokenAmounts to revert due to incorrect diff calculation. Another report will be submitted for that bug.

## Vulnerability Detail
In `executeDeposit`, when user deposit in a market with the same long and short tokens,  `getAdjustedLongAndShortTokenAmounts` is called to calculate the distribution to minimize price impact for user.
```solidity
        // if the market.longToken and market.shortToken are the same, there are two cases to consider:
        // 1. the user is depositing the market.longToken directly
        // 2. the user is depositing an initialLongToken and swapping it to the market.longToken
        // for both cases, we expect the cache.shortTokenAmount to be zero, because it is unlikely that
        // the user provides different initialLongTokens and initialShortTokens to be swapped to the same
        // token, so that flow is not supported
        // for the first case, the deposited token will be recorded in initialLongTokenAmount, it is not possible
        // to have an initialShortTokenAmount because recordTransferIn records a single difference in balance of the token
        // after all transfers
        // for both cases, split the longTokenAmount into longTokenAmount and shortTokenAmount to minimize
        // price impact for the user
        if (market.longToken == market.shortToken) {
            if (cache.shortTokenAmount > 0) {
                revert UnexpectedNonZeroShortAmount();
            }


            (cache.longTokenAmount, cache.shortTokenAmount) = getAdjustedLongAndShortTokenAmounts(
                params.dataStore,
                market,
                cache.longTokenAmount
            );
        }
```


In `getAdjustedLongAndShortTokenAmounts`, see `  adjustedLongTokenAmount = diff + (longTokenAmount - diff) / 2;`. My assumption is that the if check ` if (diff < poolLongTokenAmount) {` is to make sure diff < longTokenAmount and that `poolLongTokenAmount` is a mistake and the intended variable is `longTokenAmount`. Even if the check is required for other reasons, I believe that a diff < longTokenAmount check is necessary to prevent this revert in the event longTokenAmount is more than diff. This problem also applies to `adjustedShortTokenAmount = diff + (longTokenAmount - diff) / 2;`. 

```solidity
        if (poolLongTokenAmount < poolShortTokenAmount) {
            uint256 diff = poolLongTokenAmount - poolShortTokenAmount;


            if (diff < poolLongTokenAmount) {
                adjustedLongTokenAmount = diff + (longTokenAmount - diff) / 2;
                adjustedShortTokenAmount = longTokenAmount - adjustedLongTokenAmount;
            } else {
                adjustedLongTokenAmount = longTokenAmount;
            }
        } else {
            uint256 diff = poolShortTokenAmount - poolLongTokenAmount;


            if (diff < poolShortTokenAmount) {
                adjustedShortTokenAmount = diff + (longTokenAmount - diff) / 2;
                adjustedLongTokenAmount - longTokenAmount - adjustedShortTokenAmount;
            } else {
                adjustedLongTokenAmount = 0;
                adjustedShortTokenAmount = longTokenAmount;
            }
        }


        return (adjustedLongTokenAmount, adjustedShortTokenAmount);


```



## Impact
DOS to executeDeposit when difference of pool long token amount and pool short token amount is more than user deposit amount in a market where long token is the same as short token.

## Linked issues 
1. getAdjustedLongAndShortTokenAmounts will always revert  
2. loss of funds to user when depositing in a market that uses the same long token and short token 

## Code Snippet
https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L151-L172

https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L381-L414
## Tool used

Manual Review

## Recommendation
Recommend adding the following check

`diff < longTokenAmount` for [ExecuteDepositUtils.sol#L395](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L395) and [ExecutDepositUtils.sol#L404](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L404)