float-audits

high

# Incorrect inequality applied

## Summary

The check in `MarketUtils.sol` for `claimCollateral()` for `adjustedClaimableAmount` and `claimedAmount` has an incorrect inequality.

## Vulnerability Detail

The check for whether the user has already claimed the claimable collateral amount is incorrect.
The contract will error out if the condition `if (adjustedClaimableAmount >= claimedAmount)` is not met, because of the following assignment of `uint256 remainingClaimableAmount = adjustedClaimableAmount - claimedAmount;`

## Impact

This means that a user cannot claim their collateral from the protocol and hence loss of user funds

## Code Snippet

Loc: https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/market/MarketUtils.sol#L618-L636

```solidity
function claimCollateral(
    DataStore dataStore,
    EventEmitter eventEmitter,
    address market,
    address token,
    uint256 timeKey,
    address account,
    address receiver
) internal {
    uint256 claimableAmount = dataStore.getUint(Keys.claimableCollateralAmountKey(market, token, timeKey, account));
    uint256 claimableFactor = dataStore.getUint(Keys.claimableCollateralFactorKey(market, token, timeKey, account));
    uint256 claimedAmount = dataStore.getUint(Keys.claimedCollateralAmountKey(market, token, timeKey, account));

    uint256 adjustedClaimableAmount = Precision.applyFactor(claimableAmount, claimableFactor);
    if (adjustedClaimableAmount >= claimedAmount) {
        revert CollateralAlreadyClaimed(adjustedClaimableAmount, claimedAmount);
    }

    uint256 remainingClaimableAmount = adjustedClaimableAmount - claimedAmount;

    dataStore.setUint(
        Keys.claimedCollateralAmountKey(market, token, timeKey, account),
        adjustedClaimableAmount
    );

    MarketToken(payable(market)).transferOut(
        token,
        receiver,
        remainingClaimableAmount
    );

    MarketEventUtils.emitCollateralClaimed(
        eventEmitter,
        market,
        token,
        timeKey,
        account,
        receiver,
        remainingClaimableAmount
    );
}
```

## Tool used

Manual Review

## Recommendation
Change line 632 to `if (adjustedClaimableAmount <= claimedAmount) {`