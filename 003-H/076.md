rvierdiiev

high

# MarketUtils.claimCollateral implemented incorrectly

## Summary
MarketUtils.claimCollateral implemented incorrectly. Because of that it will be impossible to claim collateral.
## Vulnerability Detail
When user wants to claim collateral, then `ExchangeRouter.claimCollateral` calls `MarketUtils.claimCollateral` function.
https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/market/MarketUtils.sol#L618-L658
```solidity
    function claimCollateral(
        DataStore dataStore,
        EventEmitter eventEmitter,
        address market,
        address token,
        uint256 timeKey,
        address account,
        address receiver
    ) internal {
        uint256 claimableAmount = dataStore.getUint(Keys.claimableCollateralAmountKey(market, token, timeKey, account));
        uint256 claimableFactor = dataStore.getUint(Keys.claimableCollateralFactorKey(market, token, timeKey, account));
        uint256 claimedAmount = dataStore.getUint(Keys.claimedCollateralAmountKey(market, token, timeKey, account));


        uint256 adjustedClaimableAmount = Precision.applyFactor(claimableAmount, claimableFactor);
        if (adjustedClaimableAmount >= claimedAmount) {
            revert CollateralAlreadyClaimed(adjustedClaimableAmount, claimedAmount);
        }


        uint256 remainingClaimableAmount = adjustedClaimableAmount - claimedAmount;


        dataStore.setUint(
            Keys.claimedCollateralAmountKey(market, token, timeKey, account),
            adjustedClaimableAmount
        );


        MarketToken(payable(market)).transferOut(
            token,
            receiver,
            remainingClaimableAmount
        );


        MarketEventUtils.emitCollateralClaimed(
            eventEmitter,
            market,
            token,
            timeKey,
            account,
            receiver,
            remainingClaimableAmount
        );
    }
```

The problem in this function is following condition: 
```solidity
        if (adjustedClaimableAmount >= claimedAmount) {
            revert CollateralAlreadyClaimed(adjustedClaimableAmount, claimedAmount);
        }
```
It's implemented incorrectly, as it actually doesn't allow user to claim. In case if `adjustedClaimableAmount` is bigger than `claimedAmount`, that means that user now should be able to claim `adjustedClaimableAmount - claimedAmount` amount. But function doesn't allow that.
## Impact
MarketUtils.claimCollateral is not working.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Change condition to:
```solidity
        if (adjustedClaimableAmount <= claimedAmount) {
            revert CollateralAlreadyClaimed(adjustedClaimableAmount, claimedAmount);
        }
```