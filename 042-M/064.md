rvierdiiev

medium

# ExecuteDepositUtils.getAdjustedLongAndShortTokenAmounts works incorrectly

## Summary
ExecuteDepositUtils.getAdjustedLongAndShortTokenAmounts works incorrectly. It thinks that short and long token pool amount is not same and also has typo which leads to revert of function.
## Vulnerability Detail
ExecuteDepositUtils.getAdjustedLongAndShortTokenAmounts function [will be called](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L162-L172), only when `market.longToken == marketShortToken`.

It is used to `calculate the long and short amounts that would lead to the smallest amount of price impact by helping to balance the pool`(is taken from comments in the code).

https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L381-L414
```solidity
    function getAdjustedLongAndShortTokenAmounts(
        DataStore dataStore,
        Market.Props memory market,
        uint256 longTokenAmount
    ) internal view returns (uint256, uint256) {
        uint256 poolLongTokenAmount = MarketUtils.getPoolAmount(dataStore, market.marketToken, market.longToken);
        uint256 poolShortTokenAmount = MarketUtils.getPoolAmount(dataStore, market.marketToken, market.shortToken);


        uint256 adjustedLongTokenAmount;
        uint256 adjustedShortTokenAmount;


        if (poolLongTokenAmount < poolShortTokenAmount) {
            uint256 diff = poolLongTokenAmount - poolShortTokenAmount;


            if (diff < poolLongTokenAmount) {
                adjustedLongTokenAmount = diff + (longTokenAmount - diff) / 2;
                adjustedShortTokenAmount = longTokenAmount - adjustedLongTokenAmount;
            } else {
                adjustedLongTokenAmount = longTokenAmount;
            }
        } else {
            uint256 diff = poolShortTokenAmount - poolLongTokenAmount;


            if (diff < poolShortTokenAmount) {
                adjustedShortTokenAmount = diff + (longTokenAmount - diff) / 2;
                adjustedLongTokenAmount - longTokenAmount - adjustedShortTokenAmount;
            } else {
                adjustedLongTokenAmount = 0;
                adjustedShortTokenAmount = longTokenAmount;
            }
        }


        return (adjustedLongTokenAmount, adjustedShortTokenAmount);
    }
```


Later this function [fetches pool amount](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/ExecuteDepositUtils.sol#L386-L387) of market's long and short tokens. But that doesn't make sense, as because market in this case has same long and short token, then pool amount for them will be same.

So that means that `else` clause of the `if` block will be always called and `diff` will be always 0.

Now let's check part of `else` block.
```solidity
 if (diff < poolShortTokenAmount) {
                adjustedShortTokenAmount = diff + (longTokenAmount - diff) / 2;
                adjustedLongTokenAmount - longTokenAmount - adjustedShortTokenAmount;
            } else {
                adjustedLongTokenAmount = 0;
                adjustedShortTokenAmount = longTokenAmount;
            }
```

There is a typo here `adjustedLongTokenAmount - longTokenAmount - adjustedShortTokenAmount;` which should be `adjustedLongTokenAmount = longTokenAmount - adjustedShortTokenAmount;`. Because of that, the function will always revert here with underflow error.
## Impact
ExecuteDepositUtils.getAdjustedLongAndShortTokenAmounts will always revert.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
You don't need to check pool 2 times here as longToken == shortToken. Also fix typo so function doesn't revert.