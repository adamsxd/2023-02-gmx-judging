rvierdiiev

medium

# Price impact is calculated not in same way as described in the docs

## Summary
Price impact is calculated not in same way as described in the docs
## Vulnerability Detail
`SwapPricingUtils._getPriceImpactUsd` function is used to calculate [price impact](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/pricing/SwapPricingUtils.sol#L92).

Docs say:
https://github.com/gmx-io/gmx-synthetics#price-impact
> Price impact is calculated as:
`(initial USD difference) ^ (price impact exponent) * (price impact factor / 2) - (next USD difference) ^ (price impact exponent) * (price impact factor / 2)`

But in the code it works not like that.
https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/pricing/SwapPricingUtils.sol#L123-L156
```solidity
    function _getPriceImpactUsd(DataStore dataStore, address market, PoolParams memory poolParams) internal view returns (int256) {
        uint256 initialDiffUsd = Calc.diff(poolParams.poolUsdForTokenA, poolParams.poolUsdForTokenB);
        uint256 nextDiffUsd = Calc.diff(poolParams.nextPoolUsdForTokenA, poolParams.nextPoolUsdForTokenB);


        // check whether an improvement in balance comes from causing the balance to switch sides
        // for example, if there is $2000 of ETH and $1000 of USDC in the pool
        // adding $1999 USDC into the pool will reduce absolute balance from $1000 to $999 but it does not
        // help rebalance the pool much, the isSameSideRebalance value helps avoid gaming using this case
        bool isSameSideRebalance = (poolParams.poolUsdForTokenA <= poolParams.poolUsdForTokenB) == (poolParams.nextPoolUsdForTokenA <= poolParams.nextPoolUsdForTokenB);
        uint256 impactExponentFactor = dataStore.getUint(Keys.swapImpactExponentFactorKey(market));


        if (isSameSideRebalance) {
            bool hasPositiveImpact = nextDiffUsd < initialDiffUsd;
            uint256 impactFactor = dataStore.getUint(Keys.swapImpactFactorKey(market, hasPositiveImpact));


            return PricingUtils.getPriceImpactUsdForSameSideRebalance(
                initialDiffUsd,
                nextDiffUsd,
                impactFactor,
                impactExponentFactor
            );
        } else {
            uint256 positiveImpactFactor = dataStore.getUint(Keys.swapImpactFactorKey(market, true));
            uint256 negativeImpactFactor = dataStore.getUint(Keys.swapImpactFactorKey(market, false));


            return PricingUtils.getPriceImpactUsdForCrossoverRebalance(
                initialDiffUsd,
                nextDiffUsd,
                positiveImpactFactor,
                negativeImpactFactor,
                impactExponentFactor
            );
        }
    }
```

In case if `isSameSideRebalance` then function calls `PricingUtils.getPriceImpactUsdForSameSideRebalance` function, which calculates impact exactly like it's said in the docs.
But in case if `!isSameSideRebalance`, then `PricingUtils.getPriceImpactUsdForCrossoverRebalance` function is called with `positiveImpactFactor` and `negativeImpactFactor`. 
According to the docs, we have only 1 impact factor for a whole market, but inside this function we can see, that in case of crossover rebalance, then calculations use another one factor.

https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/pricing/PricingUtils.sol#L91-L105
```solidity
    function getPriceImpactUsdForCrossoverRebalance(
        uint256 initialDiffUsd,
        uint256 nextDiffUsd,
        uint256 positiveImpactFactor,
        uint256 negativeImpactFactor,
        uint256 impactExponentFactor
    ) internal pure returns (int256) {
        uint256 positiveImpactUsd = applyImpactFactor(initialDiffUsd, positiveImpactFactor, impactExponentFactor);
        uint256 negativeImpactUsd = applyImpactFactor(nextDiffUsd, negativeImpactFactor, impactExponentFactor);
        uint256 deltaDiffUsd = Calc.diff(positiveImpactUsd, negativeImpactUsd);


        int256 priceImpactUsd = Calc.toSigned(deltaDiffUsd, positiveImpactUsd > negativeImpactUsd);


        return priceImpactUsd;
    }
```
And we can see that `initialDiffUsd` use `positiveImpactFactor` while `nextDiffUsd` use `negativeImpactFactor`.
So according to this, i can state that the docs and the code calculate impact in different ways.
## Impact
Price impact is calculated in another way, than docs describes that. 
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Make docs correspond to the code.