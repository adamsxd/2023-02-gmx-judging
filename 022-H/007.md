bin2chen

high

# createDeposit() steal wnt

## Summary
createDeposit() Wrong use of market.longToken/shortToken for judgment,lead to  malicious user can use non-wnt as executionFee, but refund is get wnt,thus making a profit from it

## Vulnerability Detail
User submits a deposit request via createDeposit()
For the way executionFee is collected, there are three cases as follows.
1. When initialLongToken == wnt, it is deducted directly from initialLongTokenAmount
2. When initialShortToken == wnt, it is deducted directly from initialShortTokenAmount
3. If none of the above, take it from vault, depositVault.recordTransferIn(wnt)
The implementation code is as follows:
```solidity
    function createDeposit(
        DataStore dataStore,
        EventEmitter eventEmitter,
        DepositVault depositVault,
        address account,
        CreateDepositParams memory params
    ) external returns (bytes32) {
        Market.Props memory market = MarketUtils.getEnabledMarket(dataStore, params.market);

        uint256 initialLongTokenAmount = depositVault.recordTransferIn(params.initialLongToken);
        uint256 initialShortTokenAmount = depositVault.recordTransferIn(params.initialShortToken);

        address wnt = TokenUtils.wnt(dataStore);

        if (market.longToken == wnt) {   //@audit<---------This place is misused market.longToken，Correct should be initialLongToken
            initialLongTokenAmount -= params.executionFee;
        } else if (market.shortToken == wnt) {  //@audit <---------This place is misused market.shortToken，Correct should be initialShortToken
            initialShortTokenAmount -= params.executionFee;
        } else {
            uint256 wntAmount = depositVault.recordTransferIn(wnt);
            if (wntAmount < params.executionFee) {
                revert InsufficientWntAmountForExecutionFee(wntAmount, params.executionFee);
            }

            GasUtils.handleExcessExecutionFee(
                dataStore,
                depositVault,
                wntAmount,
                params.executionFee
            );
        }
```
There is a problem with the above implementation.
Wrong use of market.longToken/shortToken for judgment
The correct should use initialLongToken/initialShortToken

This will lead to a problem: a malicious user can specify a market.longToken==wnt, but initialLongToken==SOL(or other not wnt), so createDeposit() give SOL, but when executeDeposit() or cancelDeposit() will return wnt

If the value of initialLongToken is much smaller than wnt, and the specified executionFee is large, it will be very profitable

Example:
market = {longToken=wnt,....}
initialLongToken=SOL ,  initialLongTokenAmount=1000
initialShortToken=Token_A , initialShortTokenAmount =1
executionFee = 1000


1. alice call createDeposit() will sucess:
deposit.initialLongToken=SOL
deposit.initialLongTokenAmount=1000-1000 = 0
deposit.initialShortToken=Token_A
deposit.initialShortTokenAmount=1
deposit.executionFee = 1000

2. when executeDeposit() or cancelDeposit() will refund wnt
 executeDeposit() refund 1000-executionFeeForKeeper
cancelDeposit   refund 1000

3.so we give 1000 sol,but refund 1000 wnt

The wnt comes from the DepositVault, because all unexecuted deposits are stored in this vault, so there is a lot of balance in it



## Impact

malicious user can use non-wnt as executionFee, but refund is get wnt,thus making a profit from it

## Code Snippet

https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/deposit/DepositUtils.sol#L63-L93

## Tool used

Manual Review

## Recommendation
```solidity
    function createDeposit(
        DataStore dataStore,
        EventEmitter eventEmitter,
        DepositVault depositVault,
        address account,
        CreateDepositParams memory params
    ) external returns (bytes32) {
....

-       if (market.longToken == wnt) {
+       if (params.initialLongToken == wnt) {
            initialLongTokenAmount -= params.executionFee;
-       } else if (market.shortToken == wnt) {
+       } else if (params.initialShortToken == wnt) {
            initialShortTokenAmount -= params.executionFee;
        } else {
```
