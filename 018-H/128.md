juancito

high

# Error in the calculation of the price impact amount

## Summary

There is a miscalculation in the `getPriceImpactAmount()` function, responsible for the price impact amount for a position increase / decrease.

This ultimately affects the amount of tokens in a market's swap impact pool and the prices in every swap.

## Vulnerability Detail

The `getPriceImpactAmount()` function is incorrectly dividing the `_latestPrice` two times when calculating the price impact amount:

```solidity
             // @audit-info `size` is the change in position size.
             // @audit-info `priceDiff` is the difference between the latest price and the execution price.
182:         int256 priceImpactUsd = size.toInt256() * priceDiff / _latestPrice.toInt256(); // @audit divide by _latestPrice
183: 
184:         // round positive price impact up, this will be deducted from the position impact pool
185:         if (priceImpactUsd > 0) {
186:             return Calc.roundUpDivision(priceImpactUsd, _latestPrice); // @audit divide by _latestPrice again
187:         }
188: 
189:         // round negative price impact down, this will be stored in the position impact pool
190:         return priceImpactUsd / _latestPrice.toInt256(); // @audit divide by _latestPrice again
```

The `priceImpactUsd` should be `size * priceDiff`, and then divided once by `_latestPrice` to calculate the price impact amount.

This calculation is later used in the external functions `increasePosition()` and `decreasePosition()`, responsible for increasing or decreasing the size of a position in a market.

## Impact

Miscalculation of the delta applied to the position impact pool when increasing or decreasing a position in a market. It will ultimately affect the amount of tokens in a market's swap impact pool, and thus the prices in every swap.

## Code Snippet

```solidity
// File: gmx-synthetics/contracts/pricing/PositionPricingUtils.sol

147:     // @dev get the price impact amount for a position increase / decrease
148:     // @param size the change in position size
149:     // @param executionPrice the execution price of the index token
150:     // @param latestPrice the latest price of the index token
151:     // @param isLong whether the position is long or short
152:     // @param isIncrease whether it is an increase or decrease position
153:     // @return the price impact amount for a position increase / decrease
154:     function getPriceImpactAmount(
155:         uint256 size,
156:         uint256 executionPrice,
157:         Price.Props memory latestPrice,
158:         bool isLong,
159:         bool isIncrease
160:     ) internal pure returns (int256) {
161:         uint256 _latestPrice;
162:         if (isIncrease) {
163:             _latestPrice = isLong ? latestPrice.max : latestPrice.min;
164:         } else {
165:             _latestPrice = isLong ? latestPrice.min : latestPrice.max;
166:         }
167: 
168:         // increase order:
169:         //     - long: price impact is size * (_latestPrice - executionPrice) / _latestPrice
170:         //             when executionPrice is smaller than _latestPrice there is a positive price impact
171:         //     - short: price impact is size * (executionPrice - _latestPrice) / _latestPrice
172:         //              when executionPrice is larger than _latestPrice there is a positive price impact
173:         // decrease order:
174:         //     - long: price impact is size * (executionPrice - _latestPrice) / _latestPrice
175:         //             when executionPrice is larger than _latestPrice there is a positive price impact
176:         //     - short: price impact is size * (_latestPrice - executionPrice) / _latestPrice
177:         //              when executionPrice is smaller than _latestPrice there is a positive price impact
178:         int256 priceDiff = _latestPrice.toInt256() - executionPrice.toInt256();
179:         bool shouldFlipPriceDiff = isIncrease ? !isLong : isLong;
180:         if (shouldFlipPriceDiff) { priceDiff = -priceDiff; }
181: 
182:         int256 priceImpactUsd = size.toInt256() * priceDiff / _latestPrice.toInt256(); // @audit divide by _latestPrice
183: 
184:         // round positive price impact up, this will be deducted from the position impact pool
185:         if (priceImpactUsd > 0) {
186:             return Calc.roundUpDivision(priceImpactUsd, _latestPrice); // @audit divide by _latestPrice again
187:         }
188: 
189:         // round negative price impact down, this will be stored in the position impact pool
190:         return priceImpactUsd / _latestPrice.toInt256(); // @audit divide by _latestPrice again
191:     }
```

[Link to code](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/pricing/PositionPricingUtils.sol#L147-L191)

The first division is performed on line 182, and the second one is performed in line 185 or 190 depending on the `priceImactUsd` sign.

The `increasePosition()` and `decreasePosition()` functions call `applyDeltaToSwapImpactPool()` to alter the swap impact pool:

```solidity
// File: gmx-synthetics/contracts/position/DecreasePositionCollateralUtils.sol

68:          (values.executionPrice, values.priceImpactAmount, values.priceImpactDiffUsd) = getExecutionPrice(params, cache.prices, params.order.sizeDeltaUsd());

223:         // if there is a positive impact, the impact pool amount should be reduced
224:         // if there is a negative impact, the impact pool amount should be increased
225:         MarketUtils.applyDeltaToPositionImpactPool(
226:             params.contracts.dataStore,
227:             params.contracts.eventEmitter,
228:             params.market.marketToken,
229:             -values.priceImpactAmount
230:         );

314:         int256 priceImpactAmount = PositionPricingUtils.getPriceImpactAmount(
315:             sizeDeltaUsd,
316:             executionPrice,
317:             prices.indexTokenPrice,
318:             params.position.isLong(),
319:             false
320:         );
```

[Link to code](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/position/DecreasePositionCollateralUtils.sol#L223-L230)

```solidity
// File: gmx-synthetics/contracts/position/IncreasePositionUtils.sol

97:          (cache.executionPrice, cache.priceImpactAmount) = getExecutionPrice(params, prices);
98:
99:         // if there is a positive impact, the impact pool amount should be reduced
100:         // if there is a negative impact, the impact pool amount should be increased
101:         MarketUtils.applyDeltaToPositionImpactPool(
102:             params.contracts.dataStore,
103:             params.contracts.eventEmitter,
104:             params.market.marketToken,
105:             -cache.priceImpactAmount
106:         );

323:         int256 priceImpactAmount = PositionPricingUtils.getPriceImpactAmount(
324:             params.order.sizeDeltaUsd(),
325:             executionPrice,
326:             prices.indexTokenPrice,
327:             params.order.isLong(),
328:             true
329:         );
```

[Link to code](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/position/IncreasePositionUtils.sol#L99-L106)

The `applyDeltaToSwapImpactPool()` function updates the `swapImpactPoolAmountKey`.

```solidity
// File: gmx-synthetics/contracts/market/MarketUtils.sol

742:     // @dev apply a delta to the swap impact pool
743:     // @param dataStore DataStore
744:     // @param eventEmitter EventEmitter
745:     // @param market the market to apply to
746:     // @param token the token to apply to
747:     // @param delta the delta amount
748:     function applyDeltaToSwapImpactPool(
749:         DataStore dataStore,
750:         EventEmitter eventEmitter,
751:         address market,
752:         address token,
753:         int256 delta
754:     ) internal returns (uint256) {
755:         uint256 nextValue = dataStore.applyBoundedDeltaToUint(
756:             Keys.swapImpactPoolAmountKey(market, token),
757:             delta
758:         );
759: 
760:         MarketEventUtils.emitSwapImpactPoolAmountUpdated(eventEmitter, market, token, delta, nextValue);
761: 
762:         return nextValue;
763:     }
```

[Link to code](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/market/MarketUtils.sol#L742-L763)

This affects the amount of tokens in a market's swap impact pool:

```solidity
// File: gmx-synthetics/contracts/data/Keys.sol

698:     // @dev key for amount of tokens in a market's swap impact pool
699:     // @param market the market to check
700:     // @param token the token to check
701:     // @return key for amount of tokens in a market's swap impact pool
702:     function swapImpactPoolAmountKey(address market, address token) internal pure returns (bytes32) {
703:         return keccak256(abi.encode(
704:             SWAP_IMPACT_POOL_AMOUNT,
705:             market,
706:             token
707:         ));
708:     }
```

[Link to code](https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/data/Keys.sol#L698-L708)

## Tool used

Manual Review

## Recommendation

Divide by `_latestPrice` only once. Remove the first division and keep the ones that round the result accordingly.

```diff
-       int256 priceImpactUsd = size.toInt256() * priceDiff / _latestPrice.toInt256();
+       int256 priceImpactUsd = size.toInt256() * priceDiff;
 
        // round positive price impact up, this will be deducted from the position impact pool
        if (priceImpactUsd > 0) {
            return Calc.roundUpDivision(priceImpactUsd, _latestPrice);
        }
 
        // round negative price impact down, this will be stored in the position impact pool
        return priceImpactUsd / _latestPrice.toInt256();
```
