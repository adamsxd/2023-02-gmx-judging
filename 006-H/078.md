rvierdiiev

high

# PositionPricingUtils.getPositionFees takes more fees than should

## Summary
PositionPricingUtils.getPositionFees takes more fees than should. User loses funds every order.
## Vulnerability Detail
`PositionPricingUtils.getPositionFees` function is called to calculate fees for the position.
https://github.com/sherlock-audit/2023-02-gmx/blob/main/gmx-synthetics/contracts/pricing/PositionPricingUtils.sol#L351-L400
```solidity
    function getPositionFees(
        DataStore dataStore,
        IReferralStorage referralStorage,
        Position.Props memory position,
        Price.Props memory collateralTokenPrice,
        address longToken,
        address shortToken,
        uint256 sizeDeltaUsd
    ) internal view returns (PositionFees memory) {
        PositionFees memory fees;


        (
            fees.referral.affiliate,
            fees.referral.traderDiscountAmount,
            fees.referral.affiliateRewardAmount,
            fees.feeReceiverAmount,
            fees.positionFeeAmountForPool
        ) = getPositionFeesAfterReferral(
            dataStore,
            referralStorage,
            collateralTokenPrice,
            position.account(),
            position.market(),
            sizeDeltaUsd
        );


        fees.borrowingFeeAmount = MarketUtils.getBorrowingFees(dataStore, position) / collateralTokenPrice.min;


        uint256 borrowingFeeReceiverFactor = dataStore.getUint(Keys.BORROWING_FEE_RECEIVER_FACTOR);
        uint256 borrowingFeeAmountForFeeReceiver = Precision.applyFactor(fees.borrowingFeeAmount, borrowingFeeReceiverFactor);


        fees.feeAmountForPool = fees.positionFeeAmountForPool + fees.borrowingFeeAmount - borrowingFeeAmountForFeeReceiver;
        fees.feeReceiverAmount += borrowingFeeAmountForFeeReceiver;


        int256 latestLongTokenFundingAmountPerSize = MarketUtils.getFundingAmountPerSize(dataStore, position.market(), longToken, position.isLong());
        int256 latestShortTokenFundingAmountPerSize = MarketUtils.getFundingAmountPerSize(dataStore, position.market(), shortToken, position.isLong());


        fees.funding = getFundingFees(
            position,
            longToken,
            shortToken,
            latestLongTokenFundingAmountPerSize,
            latestShortTokenFundingAmountPerSize
        );


        fees.totalNetCostAmount = fees.referral.affiliateRewardAmount + fees.feeReceiverAmount + fees.positionFeeAmountForPool + fees.funding.fundingFeeAmount + fees.borrowingFeeAmount;
        fees.totalNetCostUsd = fees.totalNetCostAmount * collateralTokenPrice.max;


        return fees;
    }
```

Let's check how `fees.totalNetCostAmount` amount is calculated for user.
`fees.totalNetCostAmount = fees.referral.affiliateRewardAmount + fees.feeReceiverAmount + fees.positionFeeAmountForPool + fees.funding.fundingFeeAmount + fees.borrowingFeeAmount;`

Here `fees.referral.affiliateRewardAmount` is amount that user should pay for refferal. `fees.feeReceiverAmount` is everything that comes to fee receiver(fee for receiver for position + borrowing fee for receiver). `fees.positionFeeAmountForPool` is position fee for pool(fee for borrowing for pool is not included). `fees.funding.fundingFeeAmount` is funding fee amount. `fees.borrowingFeeAmount` is full fee for borrowing(borrowing fee for receiver, which is already included inside `fees.feeReceiverAmount` + fee for pool).

As you can see, borrowing fee for receiver is included 2 times here inside `fees.feeReceiverAmount` and `fees.borrowingFeeAmount` variables. As result user pays more fees than he should every time.
## Impact
User pays more fees
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Do not include borrowing fee for receiver 2 times.
`fees.totalNetCostAmount = fees.referral.affiliateRewardAmount + fees.feeReceiverAmount + fees.feeAmountForPool + fees.funding.fundingFeeAmount;`